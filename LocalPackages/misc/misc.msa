#
# Utilities
#

/each $ = >>>
        assign(@args, parse_args($))
        foreach(all_players(), @p, g(
                run(replace($, '^', @p))
        ))
<<<

/kickall $ = >>>
	foreach(all_players(), @p, g(
		pkick(@p, $)
	))
<<<

/kickforrestart = die('no.'); #call_alias('/kickall Server restarting.')

#/runas $player $ = runas($player, $)

/reloadit = runas(~op, /reload)

/ra = /reloadaliases

#
# Commands for unprivileged users
#

*:/compass = >>>
    assign(@l, mod(floor(divide(add(pfacing()[0], 22.5), 45)), 8))
    if(array_contains(array(3,4,5), @l), assign(@o, concat(@o, 'N')))
    if(array_contains(array(7,0,1), @l), assign(@o, concat(@o, 'S')))
    if(array_contains(array(1,2,3), @l), assign(@o, concat(@o, 'W')))
    if(array_contains(array(5,6,7), @l), assign(@o, concat(@o, 'E')))
    msg(concat(color(gray), 'Bearing: ', @o, ' (', mod(add(integer(pfacing()[0]),180),360), ' degrees)'))
<<<

*:/coords = >>>
    assign(@l, ploc())
    assign(@w, array('world': 'overworld', 'world_nether': 'nether', 'world_the_end': 'end'))
    msg(concat(color(gray), 'You are in the ', @w[@l[3]], '.'))
    msg(concat(color(gray), 'X: ', integer(@l[0]), ' (+East <-> -West)'))
    msg(concat(color(gray), 'Y: ', integer(@l[1]), ' (+Up <-> -Down)'))
    msg(concat(color(gray), 'Z: ', integer(@l[2]), ' (+South <-> -North)'))
    call_alias(/compass)
    msg(concat(color(gray), 'Pitch: ', multiply(integer(pfacing()[1]), -1), ' degrees'))
<<<

*:/yp = msg('Yaw:' pfacing()[0] '-' 'Pitch:' pfacing()[1])

*:/helpreq $ = /modreq $
*:/rules = msg('Rules can be found at nerd.nu/rules and on the noticeboards at spawn. Use /rulebook for a hand held guide.')
*:/ping = msg(concat(color(gray), 'Pong!'))

*:/modlist = >>>
	assign(@mods, array())
        foreach(all_players(), @p,
		if (is_null(get_value('list.hide.'.to_lower(@p))) && has_permission(@p, 'nerdch.level.mod')) {
			array_push(@mods, @p)
		}
	)
        msg(concat(color(dark_aqua), 'Online Mods: ', color(white), array_implode(@mods, ', ')))
<<<


*:/lag off = >>>
	msg(color(gray).'Lag disabled.')
<<<
*:/lag on = >>>
	set_timeout(10000, closure(
		msg(color(gray).'Lag enabled.')
	))
<<<


// This has been replaced with NerdList.
/*
*:'/list-hide' [$player] = >>>
	@p = if($player == '', player(), $player)
	_assertperm('admin')
	store_value('list.hide.'.to_lower(@p), set)
	msg(color(GOLD).@p.' will be hidden from the player list. Remember to vanish.')
<<<

*:'/list-show' [$player] = >>>
	@p = if($player == '', player(), $player)
	_assertperm('admin')
	clear_value('list.hide.'.to_lower(@p))
	msg(color(GOLD).@p.' will be shown in the player list.')
<<<

*:/list [$] = >>>
	assign(@all, array())
	assign(@players, all_players())
	array_sort(@players)
	foreach(@players, @p,
		@isShown = is_null(get_value('list.hide.'.to_lower(@p)))
		@bypass = has_permission('nerdch.level.admin')
		# I don't know why || doesn't work >.>
		#console('????@iS: '.@isShown.' @b: '.@bypass.' ??? '.or(@isShown, @bypass))	
		if (@isShown){
			assign(@group, pgroup(@p))
			assign(@group, if(array_index_exists(@group, 0), @group[0], 'default'))
			if(not(array_index_exists(@all, @group)), array_set(@all, @group, array()))
			array_push(@all[@group], @p)
		}
	)
	foreach(array('default', 'headadmins', 'techadmins', 'cadmins', 'sadmins', 'padmins', 'eadmins', 'moderators', 'modmode'), @key,
		if(array_index_exists(@all, @key)){
			assign(@coloredArray, array())
			assign(@count, 0)
			foreach(@all[@key], @p,
				@count++
				array_push(@coloredArray, if(@count % 2 == 0, color(gray), color(white)) . @p)
			)
			msg(color(gold) . @key . ': ' . array_implode(@coloredArray, ' '))
		}
	)
	msg(concat(color(gray), 'Total: ', color(white), array_size(@players), ' players'))
<<<
*/

*:'/list-all' = /list all


#TODO: Do we need these?

#/lup $player = pfacing($player, pfacing($player)[0], -90)
#/ldown $player = pfacing($player, pfacing($player)[0], 90)
#/lrev $player = pfacing($player, subtract(pfacing($player)[0], 180), pfacing($player)[1])
#/lleft $player = pfacing($player, subtract(pfacing($player)[0], 90), pfacing($player)[1])
#/lright $player = pfacing($player, add(pfacing($player)[0], 90), pfacing($player)[1])

#TODO: Do we want these?

*:/me drinks = >>>
	set_peffect(player(), 9, 1, 30)
	run('/me drinks')
<<<

#*:/drunk = set_peffect(player(), 9, 1, 30) msg(concat(color(gold), 'You been drinkin?'))
#*:/sniper = set_peffect(player(), 2, 10)
#*:/sober = set_peffect(player(), 9, 1, 0)

*:/cinfo [$] = >>>
    _assertperm('cinfo', 0)
    run('/cinfo')
<<<

*:/lwc info = call_alias('/cinfo')
*:/lwc -info = call_alias('/cinfo')
*:/lwc owners = call_alias('/cinfo')
*:/lwc owner = call_alias('/cinfo')
*:/lwc i = call_alias('/cinfo')
*:/lwc -i = call_alias('/cinfo')
*:/lwc -o = call_alias('/cinfo')
*:/lwc o = call_alias('/cinfo')

/tpi [$] = call_alias('/tpinfo' $)

*:/facing [$yaw] [$pitch] = >>>
	_assertperm('restricted', 1)
	if(equals($yaw, ''),
		msg('Usage: /facing <yaw> [pitch]'),
	#else:
		if(equals($pitch, ''),
			pfacing($yaw, pfacing()[1]),
		#else:
			pfacing($yaw, $pitch)
		)
	)
<<<

/mobtower $mob [$height=5] = >>>
	_assertperm('admin')
	assign(@lastRider, null)
	foreach(range($height), @i,
		assign(@lastSpawned, spawn_mob($mob, 1, pcursor())[0])
		if(@lastRider != null){
			set_entity_rider(@lastRider, @lastSpawned)
		}
		assign(@lastRider, @lastSpawned)
	)
<<<

/mobride $mob $player = >>>
	_assertperm('admin')
	set_ploc($player, ploc())
	set_entity_rider(spawn_mob($mob)[0], pinfo($player, 13))
<<<

'/mobride-r' $mob $radius = >>>
	_assertperm('admin')
	foreach(entities_in_radius(ploc(), $radius, PLAYER), @playerId,
		if (pinfo(player(), 13) != @playerId) {
			set_entity_rider(spawn_mob($mob)[0], @playerId)
		}
	)
<<<

mods:/kick $player [$] = >>>
	# We can add people who shouldn't be kickable here.
	assign(@unkickable, array('LadyCailin'))
	if(array_contains(@unkickable, player($player))){
		pkick(player(), 'NOPE, BITCH')
	} else {
		run('/kick' $player $)
	}
<<<

*:/drama [$] = >>>
	msg(color(red).'L'.color(gold).'l'.color(yellow).'a'.color(green).'m'.color(blue).'a')
<<<

*:/signtext [$line] [$] = >>>
	_assertperm('mod')
	if($line == ''){
		msg(color(red).'Usage: /signtext $line $text')
		msg('Supported color codes:')
		msg(colorize('&0&&0 &1&&1 &2&&2 &3&&3 &4&&4 &5&&5 &6&&6 &7&&7'))
		msg(colorize('&8&&8 &9&&9 &a&&a &b&&b &c&&c &d&&d &e&&e &f&&f'))
		msg('Typing && twice gives a single, literal &')
		die()
	}
	if($line < 1 || $line > 4){
		die(color(red).'The line number must be between 1 and 4!')
	}
	if(is_sign_at(pcursor())){
		@array = get_sign_text(pcursor())
		@array[$line - 1] = colorize($)
		set_sign_text(pcursor(), @array)
	} else {
		die('You aren\'t looking at a sign!')
	}

<<<


/fw $colors [$fade='white'] [$type='ball_large'] [$qty=5] = >>>
	_assertperm('admin')
	foreach(range($qty), @i,
		launch_firework(pcursor(), 
			array(colors: $colors, strength: 2, flicker: true, type: $type, fade: $fade)
		)
	)
<<<

'/player-detect' = >>>
	_assertperm('admin')
	@array = array()
	foreach(players_in_radius(15), @p,
		if(@p != player()){
			array_push(@array, @p)
		}
	)
	msg(array_implode(@array, ', '))
<<<

/lolno = /lolno help

/randomthor = >>>
	_assertperm('admin')
	execute(closure(lightning(ploc(array_get(array_rand(all_players(),1,false),0))))),false
<<<

/fortunecookie [$=''] = >>>
	_assertperm('admin')
	if(pinv(player(), null) != null){
		die(color(RED).'Clear the current slot in your inventory to use this command')
	}
	@fortunes = array(
		'You will be loved long time',
		'"Welcome" is a powerful word.',
		'A dubious friend may be an enemy in camouflage',
		'A feather in the hand is better than a bird in the air',
		'A fresh start will put you on your way',
		'A friend is a present you give yourself',
		'A gambler not only will lose what he has, but also will lose what he doesn\'t have',
		'A golden egg of opportunity falls into your lap this month.',
		'A good time to finish up old tasks.',
		'A hunch is creativity trying to tell you something.',
		'A light heart carries you through all the hard times.',
		'A new perspective will come with the new year.',
		'A person is never too old to learn.',
		'A person of words and not deeds is like a garden full of weeds.',
		'A pleasant surprise is waiting for you.',
		'A smile is your personal welcome mat.',
		'A smooth long journey! Great expectations.',
		'A soft voice may be awfully persuasive.',
		'A truly rich life contains love and art in abundance.',
		'You will have a bad day today',
		'Watch out for that creeper',
		'Hi Mom!',
		'Don\'t let your money get in the way of being rich',
		'Don\'t forget to brush your teeth',
		'http://i.imgur.com/psu515W.gif',
		'http://i.imgur.com/EUKubB9.jpg',
		'http://www.google.com - Where the internet finds things',
		'c.nerd.nu!',
		'p.nerd.nu!',
		's.nerd.nu!',
	)
	set_pinv(array(null: array(type: 357)))
	set_itemmeta(null, array(lore: array(if($ == '', array_rand(@fortunes, 1, false), $))))
	enchant_inv_unsafe(null, 'LOOT_BONUS_BLOCKS', 1)
<<<

#*:/nextrestart = >>>
#        assign(@nextRestartMillis, expr(get_value('nextrestart.startMillis') + 2*60*60*1000))
#        assign(@remainingSecs, round(expr((@nextRestartMillis - time()) / 1000)))
#        if (@remainingSecs < 120) {
#                msg(color(gold).sprintf('The server will restart in about %d seconds.', @remainingSecs))
#        } else {
#                assign(@remainingMins, round(expr(@remainingSecs / 60)))
#                msg(color(gold).sprintf('The server will restart in about %d minutes.', @remainingMins))
#        }
#<<<

/whoami [$] = >>>
	if(player() == 'WondrLand' && rand(0, 5) == 0){
		die('an idiot');
	}
	msg(player())
<<<

*:/whereami [$] = >>>
	msg(import('sharedata.source'));
<<<

# Used from console only.

*:/performance = >>>
	_assertperm('admin')
	run_cmd(/tps)
	run_cmd(/lag)
	console('Online: '.array_size(all_players()))
<<<
