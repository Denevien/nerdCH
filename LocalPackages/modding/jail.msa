# /setjail
#	Admin command to set the current location as the jail.

*:/setjail [$] = >>>
	_assertperm('admin')
	msg(color(GOLD).'Your current location has been saved as the jail.')
	store_value('jail.loc', ploc())
<<<


# /jail player
#	Mute the player and send them to jail.

*:/jail = >>>
	msg(color(RED).'Usage: /jail player')
	msg(color(RED).'Mute the player and send them to jail.')
<<<

*:/jail $player = >>>
	_assertperm('mod')
	@jail = get_value('jail.loc')
	if (is_null(@jail)) {
		die(color(RED).'No jail location has been set. Bug an admin.')
	}
	try(
		@player = player($player)
		run('/mute '.@player)
		set_ploc(@player, @jail)
		if (import('sharedata.source') == 'creative') {
			set_pmode(@player, SURVIVAL)
		}
		tmsg(@player, color(AQUA).'You have been jailed. Please wait for a staff member to contact you.')

		@mods = _get_mods()
		foreach(@mods, @mod,
			tmsg(@mod, color(AQUA), @player.' has been jailed by '.player())
		)
		console(@player.' has been jailed by '.player())
	,#catch
		msg(color(RED).'That player is not online.')
	)
<<<


# /release player
#	Unmute the player and sends them to spawn. It is up to the moderator to confirm
#	that the player is actually in jail before running this command.

*:/release $player = >>>
	_assertperm('mod')
	@jail = get_value('jail.loc')
	if (is_null(@jail)) {
		die(color(RED).'No jail location has been set. Bug an admin.')
	}
	try(
		@player = player($player)
		run('/unmute '.@player)
		set_ploc(@player, get_spawn('world'))
		if (import('sharedata.source') == 'creative') {
			set_pmode(@player, CREATIVE)
		}
		tmsg(@player, color(AQUA).'You have been released. Play nice!')

		@mods = _get_mods()
		foreach(@mods, @mod,
			tmsg(@mod, color(AQUA), @player.' has been released by '.player())
		)
		console(@player.' has been released by '.player())
	,#catch
		msg(color(RED).'That player is not online.')
	)
<<<
