# Create Region with proper flags and fix flags. 
# Supports multiple owners as final arguments.

*:/cregion $name $ = >>>
	if (sk_region_exists($name)) {
		die(color(RED).'A region by that name already exists.')
	} else {
		run('/region define '.$name.' '.$)
		call_alias('/addflags '.$name)
	}
<<<
*:/cregion $name = msg(color(RED).'You must specify an owner as well. /cregion <name> <owner>')

*:/addflags $name = >>>
	if (! sk_region_exists($name)) {
		die(color(RED).'There is no region by that name.')
	} else {
		run(concat('/region flag ', $name, ' use allow'))
		run(concat('/region flag ', $name, ' chest-access allow'))
		run(concat('/region flag ', $name, ' vehicle-place allow'))
		run(concat('/region flag ', $name, ' vehicle-destroy allow'))
	}
<<<


*:/members $region = >>>
        assign(@ro, array_get(sk_region_info($region, pworld()), 1))
        assign(@rm, array_get(sk_region_info($region, pworld()), 2))
        assign(@first, true)
        foreach(@ro, @player,
                if(equals(@first, true),
                        assign(@first, false)
                        assign(@final, concat(@final, @player)),
                        assign(@final, concat(@final, ', ', @player))
                )
        )
        foreach(@rm, @player,
                if(equals(@first, true),
                        assign(@first, false)
                        assign(@final, concat(@final, @player)),
                        assign(@final, concat(@final, ', ', @player))
                )
        )

        msg(concat(color(gray), 'Members of ', $region, ': ', color(white), @final))
<<<

*:/owners $region = >>>
        assign(@ro, array_get(sk_region_info($region, pworld()), 1))
        assign(@first, true)
        foreach(@ro, @player,
                if(equals(@first, true),
                        assign(@first, false)
                        assign(@final, concat(@final, @player)),
                        assign(@final, concat(@final, ', ', @player))
                )
        )
        msg(concat(color(gray), 'Owners of ', $region, ': ', color(white), @final))
<<<


# Show help for no-args invocation.

'/region-rename' = >>>
	_assertperm('restricted')
	msg(color(RED).'/region-rename oldName newName')
	msg(color(RED).'    Rename a region. The new region name must not be in use.')
	die()
<<<

'/region-rename' $oldName $newName = >>>
	_assertperm('restricted')
	if (!sk_region_exists($oldName)) {
		die(color(RED).'Region \''.$oldName.'\' does not exist in this world.')
	}
	if (sk_region_exists($newName)) {
		die(color(RED).'A region named \''.$newName.'\' already exists in this world.')
	}
	sk_region_rename($oldName,$newName)
	msg(color(GOLD).'Renamed region \''.$oldName.'\' as \''.$newName.'\'.')
<<<
