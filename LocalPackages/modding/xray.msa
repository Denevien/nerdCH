*:/nightvision = >>>
	# Allow defaults on creative and staff anywhere to use /nightvision or /nv.
	if (import('sharedata.source') != 'creative') {
		_assertperm('restricted')
	}

	# Search the player's current potion effects for night vision.
	foreach(get_peffect(player()), @effect,
		if (@effect[id] == 16) {
			# If found, remove it.
			set_peffect(player(), 16, 0, 0)
			die(color(GOLD).'Night vision disabled.')
		}
	)
	set_peffect(player(), 16, 10, 60 * 60, false)
	msg(color(GOLD).'Night vision enabled.')
<<<

*:/nv = call_alias('/nightvision')

*:/'xray-top' [$time=1d] = >>>
	_assertperm('mod', 0)
	run('/lb sum players destroyed block 56 since '.$time)
<<<

*:/'xray-edits' $player [$time=28d] = >>>
	_assertperm('mod', 0)
	run('/lb coords destroyed block 56 player '.$player.' since '.$time)
<<<

*:/'xray-rb' $player = >>>
	_assertperm('mod', 0)
	call_alias('/lbrb '.$player)
	@days = _lb_retention_days()
	@delay = 45
	set_timeout(@delay * 1000, closure(
		run('/lb redo destroyed block 14 15 16 21 56 73 74 129 player '.$player.' since '.@days.' days')
	))
	set_timeout(2 * 1000, closure(
		msg(color(GOLD).'Ore destructions by '.$player.' will be redone in '.@delay.' seconds.')
	))
<<<
