# Show the name of the player who owns the pet you are looking at.
# (Pedantically: near the block you are looking at.)

*:'/petowner' [$] = >>>
	@range = 3
	@message = color(gold).'Pets within '.@range.' blocks of that block are owned by:'
	@pets = entities_in_radius(pcursor(), @range, array(OCELOT,WOLF))
	foreach (@pets, @pet,
		if (is_tameable(@pet)) {
			@results = @results.' '.get_mob_owner(@pet).' ('.to_lower(entity_type(@pet)).')'
		}
	)
	console('petowner: at '.ploc().' '.player().' queried pets at '.pcursor().' result:'.@results, false)
	msg(@message.@results)
<<<


# Spawn a cat or dog tamed to the specified player where you are standing.

*:'/spawn-tame-mob' $type $player = >>>
	_assertperm('restricted')
	if (equals_ic($type,'DOG'), @mob = 'WOLF', @mob = $type)
	if (equals_ic(@mob,'CAT'),  @mob = 'OCELOT')
	if (!equals_ic(@mob,'OCELOT') && !equals_ic(@mob,'WOLF')) {
		die(color(red).'We can\'t spawn a '.@mob.'!')
	}

	# Prevent the mob from spawning *inside* the block at the player's feet. ;_;
	@loc = ploc()
	@loc[1] = @loc[1] + 1

	# Randomly select a cat type.
	if (equals_ic(@mob,'OCELOT')) {
		@mob = @mob.':'.array(BLACK_CAT, RED_CAT, SIAMESE_CAT)[rand(3)]
	}

	tame_mob($player, spawn_mob(@mob, 1, @loc)[0])
	msg(color(gold).'Spawned '.to_lower(@mob).' owned by '.$player.'.')
<<<


*:'/send-pet-home' [$] = >>>
	_assertperm('restricted')
	@range = 3
	@pets = entities_in_radius(pcursor(), @range, array(OCELOT,WOLF))
	if (array_size(@pets) == 0) {
		die(color(RED).'There are no pets within '.@range.' blocks of that block.')
	} else if (array_size(@pets) == 1) {
		@owner = get_mob_owner(@pets[0])
		@bed = pbed_location(@owner)
		set_ploc(@bed)
		set_entity_loc(@pets[0], @bed)
		set_timeout(3000, closure(set_entity_loc(@pets[0], @bed)))
		die(color(GOLD).'Sending '.@owner.'\'s '.to_lower(entity_type(@pets[0])).' to '.@bed.'.')
	} else {
		msg(color(RED).'There are multiple pets at that location.')
		call_alias('/petowner')
	}
<<<

